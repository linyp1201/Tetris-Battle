class Main {
    
    static Array comboTable;

    function void initialize(){
        
        /* ComboTable */
        let comboTable = Array.new(8);
        let comboTable[1] = 1;
        let comboTable[2] = 1;
        let comboTable[3] = 2;
        let comboTable[4] = 2;
        let comboTable[5] = 3;
        let comboTable[6] = 3;
        let comboTable[7] = 4;

        return;
    }

    function int Move(int ID, Map map, Block block){

        /* returns map.proc if block goes to bottom */
        
        var int key;
        var int success;
        
        let key = Keyboard.keyPressed();
        if(ID = 0 & key = 131){ //up:rotate
            let success = block.rotate();
            if(success){
                do block.setLast(true);
            }
        }
        if(ID = 1 & key = 87){ //up:rotate
            let success = block.rotate();
            if(success){
                do block.setLast(true);
            }
        }
        if(ID = 0 & key = 130){ //left
            do block.left();
            do block.setLast(false);
        }
        if(ID = 1 & key = 65){ //left
            do block.left();
            do block.setLast(false);
        }
        if(ID = 0 & key = 133){ //down
            do block.down();
            do block.setLast(false);
        }
        if(ID = 1 & key = 83){ //down
            do block.down();
            do block.setLast(false);
        }
        if(ID = 0 & key = 132){ //right
            do block.right();
            do block.setLast(false);
        }
        if(ID = 1 & key = 68){ //right
            do block.right();
            do block.setLast(false);
        }

        if(block.bottomChecker()){
            return map.proc();
        }
        else{
            return -1;
        }
    }

    function int Bonus(int prev, int TSpin, int cleared){
        if(prev = 0){
            if(TSpin = 0){
                if(cleared = 2){
                    return 1;
                }
                if(cleared = 3){
                    return 2;
                }
                if(cleared = 4){
                    return 4;
                }
            }
            if(TSpin = 1){
                if(cleared = 0){
                    return 1;
                }
                if(cleared = 1){
                    return 2;
                }
                if(cleared = 2){
                    return 4;
                }
                if(cleared = 3){
                    return 6;
                }
            }
        }
        if(prev = 1){
            /* B2B */
            if(cleared = 4){
                return 6;
            }
            if(TSpin = 1){
                if(cleared = 0){
                    return 2;
                }
                if(cleared = 1){
                    return 3;
                }
                if(cleared = 2){
                    return 6;
                }
                if(cleared = 3){
                return 9;
                }
            }
        }
        return 0;
    }

    function Array Shuffle(int newSeed){
        var Array next;
        var int index;
        var int num;
        let next = Array.new(7);
        let num = 6;
        while(num > -1){
            let newSeed = newSeed + 9;
            do Random.setSeed(newSeed);
            let index = Random.randRange(6);
            if(next[index] = 0){
                let next[index] = num;
                let num = num - 1;
            }
        }
        return next;
    }

    function void Play(String pn0, String pn1){

        /* A Game */

        var Playa P0, P1;
        var Map map0, map1;
        var Array nextSet0, nextSet1;
        var Block block0, block1;
        var int bIndex0, bIndex1;
        var int cleared0, cleared1;
        var boolean prev0, prev1;
        var int bonus0, bonus1;
        var boolean TSpin0, TSpin1;
        var int temp;
        var boolean KO;
        var Clock timer;
        var int stop;

        do Display.gameScreen();

        let stop = 0;
        let timer = Clock.new();
        let KO = false;
        let P0 = Playa.new(0, pn0);
        let P1 = Playa.new(1, pn1);//
        let map0 = Map.new();
        let map1 = Map.new();//
        let bIndex0 = 1;
        let bIndex1 = 1;//
        let prev0 = false;
        let prev1 = false;

        let nextSet0 = Main.Shuffle(timer.getTime());
        let nextSet1 = Main.Shuffle(timer.getTime() + 1);//
        let block0 = Block.new(map0, 1, 4, nextSet0[0]);
        let block1 = Block.new(map1, 1, 4, nextSet1[0]);//

        while(stop < 240 & (~KO)){

            /* showMap */
            do map0.showMap(0);
            do map1.showMap(1);
            /* showNextBlock */
            do Display.showNext(0, nextSet0[bIndex0]);
            do Display.showNext(1, nextSet1[bIndex1]);
            /* showScores */
            do Display.showScores(P0, P1);

            /* One Move */
            let cleared0 = Main.Move(0, map0, block0);
            if(cleared0 > -1){
                do P0.addLinesSent(cleared0);
            }
            let cleared1 = Main.Move(1, map1, block1);
            if(cleared1 > -1){
                do P1.addLinesSent(cleared1);
            }

            if(timer.getTime() = 500){
                do block.down();
                do block.setLast(false);
                do timer.setTime(0);
                let stop = stop + 1;
            }

            /* If toBottom, Calculate combo & sent/received lines ; else add received */
            if(cleared0 > -1){

                do Memory.deAlloc(block0);
                let block0 = Block.new(map0, 0, 4, nextSet0[bIndex0]);
                let bIndex0 = bIndex0 + 1;

                if(cleared0 = 0){
                    do P0.resetCombo();
                }
                else{
                    do P0.incCombo();
                }

                if(cleared0 > 0){
                    let temp = P0.getCombo();
                    if(temp > 7){
                        let temp = 7;
                    }
                    let TSpin0 = block0.Tspin();
                    let prev0 = (TSpin0 | (cleared0 > 3));
                    let bonus0 = Main.Bonus(prev0, TSpin0, cleared0);
                    let cleared0 = comboTable[temp] + bonus0;
                    if(map0.getHeight() = 0){
                        let cleared0 = cleared0 + 10;
                    }
                }

                if(P0.getLinesReceived() > cleared0){
                    do P0.addLinesReceived(-cleared0);
                    do P0.getGarbage(map0);
                }
                else{
                    let cleared0 = cleared0 - P0.getLinesReceived();
                }
                do P0.resetLinesReceived();
            }
            if(cleared1 > -1){

                do Memory.deAlloc(block1);
                let block1 = Block.new(map1, 0, 4, nextSet1[bIndex1]);
                let bIndex1 = bIndex1 + 1;

                if(cleared1 = 0){
                    do P1.resetCombo();
                }
                else{
                    do P1.incCombo();
                }

                if(cleared1 > 0){
                    let temp = P1.getCombo();
                    if(temp > 7){
                        let temp = 7;
                    }
                    let TSpin1 = block1.Tspin();
                    let prev1 = (TSpin1 | (cleared1 > 3));
                    let bonus1 = Main.Bonus(prev1, TSpin1, cleared1);
                    let cleared1 = comboTable[temp] + bonus1;
                    if(map0.getHeight() = 0){
                        let cleared0 = cleared0 + 10;
                    }
                }

                if(P1.getLinesReceived() > cleared1){
                    do P1.addLinesReceived(-cleared1);
                    do P1.getGarbage(map1);
                }
                else{
                    let cleared1 = cleared1 - P1.getLinesReceived();
                }
                do P1.resetLinesReceived();
            }

            if(cleared0 > 0){
                do P1.addLinesReceived(cleared0);
            }
            if(cleared1 > 0){
                do P0.addLinesReceived(cleared1);
            }

            /* tick */
            do timer.tick(1);

            /* Shuffle if needed */
            if(bIndex0 > 6){
                do nextSet0.dispose();
                let nextSet0 = Main.Shuffle(timer.getTime());
                let bIndex0 = 0;
            }
            if(bIndex1 > 6){
                do nextSet1.dispose();
                let nextSet1 = Main.Shuffle(timer.getTime() + 1);
                let bIndex1 = 0;
            }

            /* check if KO */ 
            if(map0.dead() = true){
                do P1.incKO();
                do P0.clearGarbage(map0);
            }
            if(map1.dead() = true){
                do P0.incKO();
                do P1.clearGarbage(map1);
            }
            let KO = (P0.getKO() > 4 | P1.getKO() > 4);
        }
        do Display.endScreen(P0, P1, map0.getHeight(), map1.getHeight());
        return;
    }

    function boolean TitleScreen(){
        var String pn0;
        var String pn1;
        var int endGame;
        var int curOption;
        var char key;

        let curOption = 1;
        let endGame = 0;
        let pn0 = String.new(20);
        let pn1 = String.new(20);
        let key = 0;

        while(~(key = 128)){
            let curOption = 1 - curOption;
            do Display.titleScreen(curOption);
            let key = Keyboard.readChar();
        }

        if(curOption = 0){
            do Screen.clearScreen();
            do Output.moveCursor(9, 22);
            let pn0 = Keyboard.readLine("PLAYER ZERO NAME : ");
            do Output.moveCursor(12, 22);
            let pn1 = Keyboard.readLine("PLAYER ONE NAME : ");
            do Main.Play(pn0, pn1);
        }
        else{
            let endGame = 1;
        }

        return endGame;
    }

    function void main(){
        var int endGame;
        let endGame = 0;
        while(endGame = 0){
            let endGame = Main.TitleScreen();
        }     
        return;
    }

    
}