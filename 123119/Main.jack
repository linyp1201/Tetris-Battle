class Main {
    
    static Array comboTable;

    function void initialize(){
        
        /* ComboTable */
        let comboTable = Array.new(8);
        let comboTable[1] = 1;
        let comboTable[2] = 1;
        let comboTable[3] = 2;
        let comboTable[4] = 2;
        let comboTable[5] = 3;
        let comboTable[6] = 3;
        let comboTable[7] = 4;

        return;
    }

    function int mod500(int k) /* % function */
    {
        var int q, r;
        let q = k/500;
        let q = q*500;
        let r = k - q;
        return r;
    }

    function int Move(int ID, Map map, Block block){

        /* returns map.proc if block goes to bottom */
        
        var int key;
        var int success;
        
        let key = Keyboard.keyPressed();
        if(ID = 0 & key = 131){ //up:rotate
            let success = block.rotate(map);
            if(success){
                do block.setLast(true);
            }
        }
        if(ID = 1 & key = 87){ //up:rotate
            let success = block.rotate(map);
            if(success){
                do block.setLast(true);
            }
        }
        if(ID = 0 & key = 130){ //left
            do block.left(map);
            do block.setLast(false);
        }
        if(ID = 1 & key = 65){ //left
            do block.left(map);
            do block.setLast(false);
        }
        if(ID = 0 & key = 133){ //down
            do block.down(map);
            do block.setLast(false);
        }
        if(ID = 1 & key = 83){ //down
            do block.down(map);
            do block.setLast(false);
        }
        if(ID = 0 & key = 132){ //right
            do block.right(map);
            do block.setLast(false);
        }
        if(ID = 1 & key = 68){ //right
            do block.right(map);
            do block.setLast(false);
        }

        if(block.bottomChecker(map)){
            return map.proc();
        }
        else{
            return -1;
        }
    }

    function int Bonus(int prev, int TSpin, int cleared){
        if(prev = 0){
            if(TSpin = 0){
                if(cleared = 2){
                    return 1;
                }
                if(cleared = 3){
                    return 2;
                }
                if(cleared = 4){
                    return 4;
                }
            }
            if(TSpin = 1){
                if(cleared = 0){
                    return 1;
                }
                if(cleared = 1){
                    return 2;
                }
                if(cleared = 2){
                    return 4;
                }
                if(cleared = 3){
                    return 6;
                }
            }
        }
        if(prev = 1){
            /* B2B */
            if(cleared = 4){
                return 6;
            }
            if(TSpin = 1){
                if(cleared = 0){
                    return 2;
                }
                if(cleared = 1){
                    return 3;
                }
                if(cleared = 2){
                    return 6;
                }
                if(cleared = 3){
                return 9;
                }
            }
        }
        return 0;
    }

    function void Play(String pn0, String pn1){

        /* A Game */

        var Playa P0, P1;
        var Map map0, map1;
        var Block block0, block1;
        var int bType0, bType1;
        var int bNext0, bNext1;
        var int cleared0, cleared1;
        var int isDead0, isDead1;
        var boolean prev0, prev1;
        var int bonus0, bonus1;
        var boolean TSpin0, TSpin1;
        var int temp;
        var boolean KO;
        var Clock timer;
        var int stop;

        do Display.gameScreen();

        let stop = 0;
        let timer = Clock.new();
        let KO = false;
        let P0 = Playa.new(0, pn0);
        let P1 = Playa.new(1, pn1);//
        let map0 = Map.new();
        let map1 = Map.new();//
        
        let prev0 = false;
        let prev1 = false;

        do Random.setSeed(0);
        let bType0 = Random.randRange(6);
        do Random.setSeed(1);
        let bType1 = Random.randRange(6);
        do Random.setSeed(2);
        let bNext0 = Random.randRange(6);
        do Random.setSeed(3);
        let bNext1 = Random.randRange(6);
        let block0 = Block.new(map0, bType0);
        let block1 = Block.new(map1, bType1);//

        while(stop < 240 & (~KO)){

            /* showMap */
            do map0.showMap(0);
            do map1.showMap(1);
            /* showNextBlock */
            do Display.showNext(0, bNext0);
            do Display.showNext(1, bNext1);
            /* showScores */
            do Display.showScores(P0, P1);

            /* One Move */
            let cleared0 = Main.Move(0, map0, block0);
            if(cleared0 > -1){
                do P0.addLinesSent(cleared0);
            }
            let cleared1 = Main.Move(1, map1, block1);
            if(cleared1 > -1){
                do P1.addLinesSent(cleared1);
            }
            //
            do Output.moveCursor(0, 0);
            do Output.printInt(timer.getTime());
            //

            if(Main.mod500(timer.getTime()) = 0){
                do block0.down(map0);
                do block0.setLast(false);
                do block1.down(map1);
                do block1.setLast(false);
                if(timer.getTime() = 30000){
                    do timer.setTime(0);
                }
                let stop = stop + 1;
            }

            /* If toBottom, Calculate combo & sent/received lines ; else add received */
            if(cleared0 > -1){
        
                do Random.setSeed(timer.getTime());
                let bType0 = bNext0;
                let bNext0 = Random.randRange(6);
                do block0.setType(bType0);
                do block0.setR(1);
                do block0.setC(4);
                
                if(cleared0 = 0){
                    do P0.resetCombo();
                }
                else{
                    do P0.incCombo();
                }

                if(cleared0 > 0){
                    let temp = P0.getCombo();
                    if(temp > 7){
                        let temp = 7;
                    }
                    let TSpin0 = block0.Tspin(map0);
                    let prev0 = (TSpin0 | (cleared0 > 3));
                    let bonus0 = Main.Bonus(prev0, TSpin0, cleared0);
                    let cleared0 = comboTable[temp] + bonus0;
                    if(map0.getHeight() = 0){
                        let cleared0 = cleared0 + 10;
                    }
                }

                if(P0.getLinesReceived() > cleared0){
                    do P0.addLinesReceived(-cleared0);
                    let isDead0 = P0.getGarbage(map0);
                }
                else{
                    let cleared0 = cleared0 - P0.getLinesReceived();
                }
                do P0.resetLinesReceived();
            }
            if(cleared1 > -1){
                
                do Random.setSeed(timer.getTime()+99);
                let bType1 = bNext1;
                let bNext1 = Random.randRange(6);
                do block1.setType(bType1);
                do block1.setR(1);
                do block1.setC(4);

                if(cleared1 = 0){
                    do P1.resetCombo();
                }
                else{
                    do P1.incCombo();
                }

                if(cleared1 > 0){
                    let temp = P1.getCombo();
                    if(temp > 7){
                        let temp = 7;
                    }
                    let TSpin1 = block1.Tspin(map1);
                    let prev1 = (TSpin1 | (cleared1 > 3));
                    let bonus1 = Main.Bonus(prev1, TSpin1, cleared1);
                    let cleared1 = comboTable[temp] + bonus1;
                    if(map0.getHeight() = 0){
                        let cleared0 = cleared0 + 10;
                    }
                }

                if(P1.getLinesReceived() > cleared1){
                    do P1.addLinesReceived(-cleared1);
                    let isDead1 = P1.getGarbage(map1); 
                }
                else{
                    let cleared1 = cleared1 - P1.getLinesReceived();
                }
                do P1.resetLinesReceived();
            }

            if(cleared0 > 0){
                do P1.addLinesReceived(cleared0);
            }
            if(cleared1 > 0){
                do P0.addLinesReceived(cleared1);
            }

            /* tick */
            do timer.tick(50);

            /* check if KO */ 
            if(isDead0 = -1){
                do P1.incKO();
                do P0.clearGarbage(map0);
            }
            if(map0.dead() = true){
                do P1.incKO();
                do map0.clear();
            }
            if(isDead1 = -1){
                do P0.incKO();
                do P1.clearGarbage(map1);
            }
            if(map1.dead() = true){
                do P0.incKO();
                do map1.clear();
            }
            let KO = ((P0.getKO() > 4) | (P1.getKO() > 4));
        }
        do Display.endScreen(P0, P1, map0.getHeight(), map1.getHeight());
        return;
    }

    function boolean TitleScreen(){
        var String pn0;
        var String pn1;
        var int endGame;
        var int curOption;
        var char key;

        let curOption = 1;
        let endGame = 0;
        let pn0 = String.new(20);
        let pn1 = String.new(20);
        let key = 0;

        while(~(key = 128)){
            let curOption = 1 - curOption;
            do Display.titleScreen(curOption);
            let key = Keyboard.readChar();
        }

        if(curOption = 0){
            do Screen.clearScreen();
            do Output.moveCursor(9, 22);
            let pn0 = Keyboard.readLine("PLAYER ZERO NAME : ");
            do Output.moveCursor(12, 22);
            let pn1 = Keyboard.readLine("PLAYER ONE NAME : ");
            do Main.Play(pn0, pn1);
        }
        else{
            let endGame = 1;
        }

        return endGame;
    }

    function void main(){
        var int endGame;
        let endGame = 0;
        while(endGame = 0){
            let endGame = Main.TitleScreen();
        }     
        return;
    }

    
}
